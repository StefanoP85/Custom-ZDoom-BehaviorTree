//==============================================================================
//
// Super Cyber Soldier Female LVL 1.
//
//==============================================================================

// -----------------------------------------------------------------------------
// AI nodes.
// -----------------------------------------------------------------------------

// Node Gesture: perform gesture animation.
class SuperCyberSoldierFemale1_AINode_Gesture : TCyberBasic_AINode_Gesture
{
	static SuperCyberSoldierFemale1_AINode_Gesture Create()
	{
		SuperCyberSoldierFemale1_AINode_Gesture Result = New('SuperCyberSoldierFemale1_AINode_Gesture');
		return Result;
	}
	override TStatus Update(TCustomCyberActor CustomCyberActor)
	{
		if (CustomCyberActor.CurrentWeapon)
			return BH_FAILURE;
		if (CustomCyberActor.IsInGestureAnimation())
			return BH_RUNNING;
		if ((CustomCyberActor.TargetAcquired) && (CustomCyberActor.PriorityQueue.Items.Size() == 0))
		{
			switch (Random[SuperCyberSoldierFemale1](1, 8))
			{
				case 1:
					CustomCyberActor.UpdateStateLabel("Idle", true);
					break;
				case 2:
					CustomCyberActor.UpdateStateLabel("Dance", true);
					break;
				case 3:
					CustomCyberActor.UpdateStateLabel("Excited", true);
					break;
				case 4:
					CustomCyberActor.UpdateStateLabel("NoWay", true);
					break;
				case 5:
					CustomCyberActor.UpdateStateLabel("Insult", true);
					break;
				case 6:
					CustomCyberActor.A_StartSound("SuperSoldierFemale/Taunt");
					CustomCyberActor.UpdateStateLabel("Insult", true);
					break;
				case 7:
					CustomCyberActor.A_StartSound("SuperSoldierFemale/Taunt");
					CustomCyberActor.UpdateStateLabel("Insult", true);
					break;
				case 8:
					CustomCyberActor.UpdateStateLabel("Idle", true);
			}
			CustomCyberActor.TargetAcquired = false;
			return BH_SUCCESS;
		}
		return BH_FAILURE;
	}
}

// Node Rest: perform idle animation.
class SuperCyberSoldierFemale1_AINode_Rest : TCyberBasic_AINode_Rest
{
	static SuperCyberSoldierFemale1_AINode_Rest Create()
	{
		SuperCyberSoldierFemale1_AINode_Rest Result = New('SuperCyberSoldierFemale1_AINode_Rest');
		return Result;
	}
	override TStatus Update(TCustomCyberActor CustomCyberActor)
	{
		CustomCyberActor.Destination = CustomCyberActor.Pos;
		if (!CustomCyberActor.IsInGestureAnimation())
			if (CustomCyberActor.CurrentWeapon)
			{
				CustomCyberActor.UpdateStateLabel("Rifle.Idle");
			}
			else
			{
				CustomCyberActor.UpdateStateLabel("Idle");
			}
		return BH_SUCCESS;
	}
}

// Node ChaseMode: decide the chase mode.
class SuperCyberSoldierFemale1_AINode_ChaseMode : TBehavior
{
	int RunFastSpeed;
	int RunSpeed;
	int WalkSpeed;
	static SuperCyberSoldierFemale1_AINode_ChaseMode Create(int WalkSpeed = 10, int RunSpeed = 20, int RunFastSpeed = 24)
	{
		SuperCyberSoldierFemale1_AINode_ChaseMode Result = New('SuperCyberSoldierFemale1_AINode_ChaseMode');
		Result.WalkSpeed = WalkSpeed;
		Result.RunSpeed = RunSpeed;
		Result.RunFastSpeed = RunFastSpeed;
		return Result;
	}
	override TStatus Update(TCustomCyberActor CustomCyberActor)
	{
		if (CustomCyberActor.HelpGiver)
		{
			CustomCyberActor.CurStateLabel = "RunFast";
			CustomCyberActor.Speed = RunFastSpeed;
			return BH_SUCCESS;
		}
		if (CustomCyberActor.HelpReceiver)
		{
			if (CustomCyberActor.HelpReceiver is 'KAI_BaseVehicle')
			{
				CustomCyberActor.CurStateLabel = "Walk";
				CustomCyberActor.Speed = WalkSpeed;
				return BH_SUCCESS;
			}
			else
			{
				CustomCyberActor.CurStateLabel = "RunFast";
				CustomCyberActor.Speed = RunFastSpeed;
				return BH_SUCCESS;
			}
		}
		if (CustomCyberActor.PriorityQueue.Items.Size() > 0)
		{
			if (CustomCyberActor.CurrentWeapon)
			{
				CustomCyberActor.UpdateStateLabel("Rifle.Run");
				CustomCyberActor.Speed = RunSpeed;
				return BH_SUCCESS;
			}
			TPriorityQueueItem PriorityQueueItem = CustomCyberActor.PriorityQueue.Items[0];
			if
			(
				(CustomCyberActor.IsIndestructible(PriorityQueueItem.Monster))
				|| (CustomCyberActor.HasGodMode(PriorityQueueItem.Monster))
				|| (CustomCyberActor.HasBuddha(PriorityQueueItem.Monster))
				|| (CustomCyberActor.IsFrightening(PriorityQueueItem.Monster))
			)
			{
				CustomCyberActor.UpdateStateLabel("RunFast");
				CustomCyberActor.Speed = RunSpeed;
				return BH_SUCCESS;
			}
			if
			(
				(PriorityQueueItem.ExperiencedThreatLevel == ETL_CRITICAL)
				|| (PriorityQueueItem.ThreatLevel > CustomCyberActor.ThreatLevelThreshold)
			)
			{
				CustomCyberActor.UpdateStateLabel("RunFast");
				CustomCyberActor.Speed = RunSpeed;
				return BH_SUCCESS;
			}
			if
			(
				(PriorityQueueItem.ExperiencedThreatLevel == ETL_DANGER)
				|| (PriorityQueueItem.ThreatLevel == CustomCyberActor.ThreatLevelThreshold)
			)
			{
				CustomCyberActor.UpdateStateLabel("RunFast");
				CustomCyberActor.Speed = RunSpeed;
				return BH_SUCCESS;
			}
			if
			(
				(PriorityQueueItem.ExperiencedThreatLevel == ETL_NORMAL)
				|| (PriorityQueueItem.ThreatLevel >= THREAT_NORMAL)
			)
			{
				CustomCyberActor.UpdateStateLabel("Run");
				CustomCyberActor.Speed = RunSpeed;
				return BH_SUCCESS;
			}
			CustomCyberActor.UpdateStateLabel("Walk");
			CustomCyberActor.Speed = WalkSpeed;
			return BH_SUCCESS;
		}
		return BH_SUCCESS;
	}
}

// Node NextMove: perform tactical movement.
class SuperCyberSoldierFemale1_AINode_NextMove : TCyberBasic_AINode_NextMove
{
	static SuperCyberSoldierFemale1_AINode_NextMove Create()
	{
		SuperCyberSoldierFemale1_AINode_NextMove Result = New('SuperCyberSoldierFemale1_AINode_NextMove');
		return Result;
	}
	override double CalculateUtilityDistance(TCustomCyberActor CustomCyberActor, TPriorityQueueItem PriorityQueueItem)
	{
		if
		(
			(CustomCyberActor.IsIndestructible(PriorityQueueItem.Monster))
			|| (CustomCyberActor.HasGodMode(PriorityQueueItem.Monster))
			|| (CustomCyberActor.HasBuddha(PriorityQueueItem.Monster))
			|| (CustomCyberActor.IsFrightening(PriorityQueueItem.Monster))
		)
			return 1048576;	// As far as possible!
		if
		(
			(PriorityQueueItem.ExperiencedThreatLevel == ETL_CRITICAL)
			|| (PriorityQueueItem.ThreatLevel > CustomCyberActor.ThreatLevelThreshold)
		)
			return 1024;
		if
		(
			(PriorityQueueItem.ExperiencedThreatLevel == ETL_DANGER)
			|| (PriorityQueueItem.ThreatLevel == CustomCyberActor.ThreatLevelThreshold)
		)
			return 384;
		if
		(
			(PriorityQueueItem.ExperiencedThreatLevel == ETL_NORMAL)
			|| (PriorityQueueItem.ThreatLevel >= THREAT_ABOVENORMAL)
		)
			return 256;
		return 128;
	}
}

// Node Fireball: check if fit for a energy attack.
class SuperCyberSoldierFemale1_AINode_Fireball : TUtilityBehavior
{
	static SuperCyberSoldierFemale1_AINode_Fireball Create()
	{
		SuperCyberSoldierFemale1_AINode_Fireball Result = New('SuperCyberSoldierFemale1_AINode_Fireball');
		return Result;
	}
	override double CalculateUtility(TCustomCyberActor CustomCyberActor)
	{
		TPriorityQueueItem PriorityQueueItem = CustomCyberActor.PriorityQueue.Items[0];
		if ((PriorityQueueItem.Monster.Health >= 5000) && (CustomCyberActor.Distance2DSquared(CustomCyberActor.Target) > 2560))
			return 0.8;
		return 0;
	}
	override TStatus Update(TCustomCyberActor CustomCyberActor)
	{
		if (CustomCyberActor.IsInAttackState())
			return BH_RUNNING;
		if (CustomCyberActor.CurrentWeapon)
		{
			CustomCyberActor.A_ChangeModel("", 1, "MODELS/SuperCyberSoldierFemale1", "NoWeapon.iqm", 1, "", "");
			CustomCyberActor.CurrentWeapon = null;
		}
		CustomCyberActor.UpdateStateLabel("Fireball", true);
		CustomCyberActor.GiveInventory('ShieldBoost', 1);
		return BH_SUCCESS;
	}
}

// Node Hand1: check if fit for a energy attack.
class SuperCyberSoldierFemale1_AINode_Hand : TUtilityBehavior
{
	static SuperCyberSoldierFemale1_AINode_Hand Create()
	{
		SuperCyberSoldierFemale1_AINode_Hand Result = New('SuperCyberSoldierFemale1_AINode_Hand');
		return Result;
	}
	override double CalculateUtility(TCustomCyberActor CustomCyberActor)
	{
		TPriorityQueueItem PriorityQueueItem = CustomCyberActor.PriorityQueue.Items[0];
		if (PriorityQueueItem.ExperiencedThreatLevel == ETL_CRITICAL)
			return 0.4;
		if (PriorityQueueItem.ExperiencedThreatLevel == ETL_DANGER)
			return 0.4;
		switch (PriorityQueueItem.ThreatLevel)
		{
			case THREAT_UNSTOPPABLE:
				return 0.4;
				break;
			case THREAT_SUPERDANGER:
				return 0.4;
				break;
			case THREAT_VERYDANGEROUS:
				return 0.4;
				break;
			case THREAT_DANGEROUS:
				return 0.4;
				break;
			case THREAT_ABOVENORMAL:
				return 0;
				break;
			case THREAT_NORMAL:
				return 0;
				break;
			case THREAT_MILD:
				return 0;
				break;
			case THREAT_LOW:
				return 0;
				break;
			case THREAT_VERYLOW:
				return 0;
				break;
			case THREAT_NONE:
				return 0;
				break;
			default:
				return 0;
				break;
		}
		return 0;
	}
	override TStatus Update(TCustomCyberActor CustomCyberActor)
	{
		if (CustomCyberActor.IsInAttackState())
			return BH_RUNNING;
		if (CustomCyberActor.CurrentWeapon)
		{
			CustomCyberActor.A_ChangeModel("", 1, "MODELS/SuperCyberSoldierFemale1", "NoWeapon.iqm", 1, "", "");
			CustomCyberActor.CurrentWeapon = null;
		}
		CustomCyberActor.UpdateStateLabel("Hand", true);
		CustomCyberActor.GiveInventory('ShieldBoost', 1);
		return BH_SUCCESS;
	}
}

// Node Hand2: check if fit for a energy attack.
class SuperCyberSoldierFemale1_AINode_Hand2 : TUtilityBehavior
{
	static SuperCyberSoldierFemale1_AINode_Hand2 Create()
	{
		SuperCyberSoldierFemale1_AINode_Hand2 Result = New('SuperCyberSoldierFemale1_AINode_Hand2');
		return Result;
	}
	override double CalculateUtility(TCustomCyberActor CustomCyberActor)
	{
		TPriorityQueueItem PriorityQueueItem = CustomCyberActor.PriorityQueue.Items[0];
		if (PriorityQueueItem.ExperiencedThreatLevel == ETL_CRITICAL)
			return 0;
		if (PriorityQueueItem.ExperiencedThreatLevel == ETL_DANGER)
			return 0;
		switch (PriorityQueueItem.ThreatLevel)
		{
			case THREAT_UNSTOPPABLE:
				return 0;
				break;
			case THREAT_SUPERDANGER:
				return 0;
				break;
			case THREAT_VERYDANGEROUS:
				return 0;
				break;
			case THREAT_DANGEROUS:
				return 0;
				break;
			case THREAT_ABOVENORMAL:
				return 0.3;
				break;
			case THREAT_NORMAL:
				return 0.3;
				break;
			case THREAT_MILD:
				return 0.3;
				break;
			case THREAT_LOW:
				return 0.3;
				break;
			case THREAT_VERYLOW:
				return 0.3;
				break;
			case THREAT_NONE:
				return 0.3;
				break;
			default:
				return 0;
				break;
		}
		return 0;
	}
	override TStatus Update(TCustomCyberActor CustomCyberActor)
	{
		if (CustomCyberActor.IsInAttackState())
			return BH_RUNNING;
		if (CustomCyberActor.CurrentWeapon)
		{
			CustomCyberActor.A_ChangeModel("", 1, "MODELS/SuperCyberSoldierFemale1", "NoWeapon.iqm", 1, "", "");
			CustomCyberActor.CurrentWeapon = null;
		}
		CustomCyberActor.UpdateStateLabel("Hand2", true);
		CustomCyberActor.GiveInventory('ShieldBoost', 1);
		return BH_SUCCESS;
	}
}

// Node Punch: check if fit for a punch attack.
class SuperCyberSoldierFemale1_AINode_Punch : TUtilityBehavior
{
	double MaxDistance; // Actually is squared for performance reasons.
	static SuperCyberSoldierFemale1_AINode_Punch Create(double MaxDistance = 128)
	{
		SuperCyberSoldierFemale1_AINode_Punch Result = New('SuperCyberSoldierFemale1_AINode_Punch');
		Result.MaxDistance = MaxDistance ** 2;
		return Result;
	}
	override double CalculateUtility(TCustomCyberActor CustomCyberActor)
	{
		if (CustomCyberActor.Distance2DSquared(CustomCyberActor.Target) <= MaxDistance)
		{
			TPriorityQueueItem PriorityQueueItem = CustomCyberActor.PriorityQueue.Items[0];
			if (PriorityQueueItem.ExperiencedThreatLevel == ETL_CRITICAL)
				return 0;
			if (PriorityQueueItem.ExperiencedThreatLevel == ETL_DANGER)
				return 0;
			switch (PriorityQueueItem.ThreatLevel)
			{
				case THREAT_UNSTOPPABLE:
					return 0;
					break;
				case THREAT_SUPERDANGER:
					return 0;
					break;
				case THREAT_VERYDANGEROUS:
					return 0;
					break;
				case THREAT_DANGEROUS:
					return 0;
					break;
				case THREAT_ABOVENORMAL:
					return 0.7;
					break;
				case THREAT_NORMAL:
					return 0.7;
					break;
				case THREAT_MILD:
					return 0.7;
					break;
				case THREAT_LOW:
					return 0.7;
					break;
				case THREAT_VERYLOW:
					return 0.7;
					break;
				case THREAT_NONE:
					return 0.7;
					break;
				default:
					return 0;
					break;
			}
			return 0;
		}
		return 0;
	}
	override TStatus Update(TCustomCyberActor CustomCyberActor)
	{
		if (CustomCyberActor.IsInAttackState())
			return BH_RUNNING;
		if (CustomCyberActor.CurrentWeapon)
		{
			CustomCyberActor.A_ChangeModel("", 1, "MODELS/SuperCyberSoldierFemale1", "NoWeapon.iqm", 1, "", "");
			CustomCyberActor.CurrentWeapon = null;
		}
		switch ( Random[SuperCyberSoldierFemale1](1, 4) )
		{
			case 1:
				CustomCyberActor.UpdateStateLabel("Punch", true);
				break;
			case 2:
				CustomCyberActor.UpdateStateLabel("QuadPunch", true);
				break;
			case 3:
				CustomCyberActor.UpdateStateLabel("KickFront", true);
				break;
			case 4:
				CustomCyberActor.UpdateStateLabel("KickLateral", true);
				break;
		}
		CustomCyberActor.GiveInventory('ShieldBoost', 1);
		return BH_SUCCESS;
	}
}

// Node Stomp: check if fit for stomping smaller foes.
class SuperCyberSoldierFemale1_AINode_Stomp : TUtilityBehavior
{
	double MaxDistance; // Actually is squared for performance reasons.
	static SuperCyberSoldierFemale1_AINode_Stomp Create(double MaxDistance = 128)
	{
		SuperCyberSoldierFemale1_AINode_Stomp Result = New('SuperCyberSoldierFemale1_AINode_Stomp');
		Result.MaxDistance = MaxDistance ** 2;
		return Result;
	}
	override double CalculateUtility(TCustomCyberActor CustomCyberActor)
	{
		Actor Target = CustomCyberActor.Target;
		if (CustomCyberActor.Distance2DSquared(CustomCyberActor.Target) <= MaxDistance)
			if ((Target.Height <= 60) && (Target.default.Health <= 256))
				return 0.9;
		return 0;
	}
	override TStatus Update(TCustomCyberActor CustomCyberActor)
	{
		if (CustomCyberActor.IsInAttackState())
			return BH_RUNNING;
		if (CustomCyberActor.CurrentWeapon)
		{
			CustomCyberActor.A_ChangeModel("", 1, "MODELS/SuperCyberSoldierFemale1", "NoWeapon.iqm", 1, "", "");
			CustomCyberActor.CurrentWeapon = null;
		}
		CustomCyberActor.UpdateStateLabel("Stomp", true);
		CustomCyberActor.GiveInventory('ShieldBoost', 1);
		return BH_SUCCESS;
	}
}

// Node CombatLG: check if fit for LightningGun usage.
class SuperCyberSoldierFemale1_AINode_CombatLG : TUtilityBehavior
{
	double MaxDistance; // Actually is squared for performance reasons.
	static SuperCyberSoldierFemale1_AINode_CombatLG Create(double MaxDistance = 384)
	{
		SuperCyberSoldierFemale1_AINode_CombatLG Result = New('SuperCyberSoldierFemale1_AINode_CombatLG');
		Result.MaxDistance = MaxDistance ** 2;
		return Result;
	}
	override double CalculateUtility(TCustomCyberActor CustomCyberActor)
	{
		TPriorityQueueItem PriorityQueueItem = CustomCyberActor.PriorityQueue.Items[0];
		if (PriorityQueueItem.ExperiencedThreatLevel == ETL_CRITICAL)
			return 0;
		if (PriorityQueueItem.ExperiencedThreatLevel == ETL_DANGER)
			return 0;
		if (CustomCyberActor.Distance2DSquared(CustomCyberActor.Target) <= MaxDistance)
		{
			if ((CustomCyberActor.Target.Health <= 70) || (CustomCyberActor.Target.Player))
				return 0.6;
			switch (PriorityQueueItem.ThreatLevel)
			{
				case THREAT_UNSTOPPABLE:
					return 0;
					break;
				case THREAT_SUPERDANGER:
					return 0;
					break;
				case THREAT_VERYDANGEROUS:
					return 0;
					break;
				case THREAT_DANGEROUS:
					return 0.6;
					break;
				case THREAT_ABOVENORMAL:
					return 0.6;
					break;
				case THREAT_NORMAL:
					return 0.6;
					break;
				case THREAT_MILD:
					return 0.6;
					break;
				case THREAT_LOW:
					return 0.6;
					break;
				case THREAT_VERYLOW:
					return 0.6;
					break;
				case THREAT_NONE:
					return 0.6;
					break;
				default:
					return 0;
					break;
			}
			return 0;
		}
		return 0;
	}
	override TStatus Update(TCustomCyberActor CustomCyberActor)
	{
		if (CustomCyberActor.WeaponCooldown == 0)
		{
			CustomCyberActor.UpdateStateLabel("Rifle.Fire", true);
			if (CustomCyberActor.CurrentWeapon != 'LightningGun')
			{
				CustomCyberActor.A_ChangeModel("", 1, "MODELS/SuperCyberSoldierFemale1", "LightningGun.iqm", 1, "MODELS/LightningGun", "LightningGun.png");
				CustomCyberActor.CurrentWeapon = 'LightningGun';
				CustomCyberActor.WeaponCooldown = 2;
				return BH_RUNNING;
			}
			CustomCyberActor.A_StartSound("weapons/lg", CHAN_WEAPON);
			CustomCyberActor.B_Missile('LightningGunProjectile', 64);
			CustomCyberActor.WeaponCooldown = 4;
		}
		return BH_SUCCESS;
	}
}

// Node CombatCR: check if fit for CyberRifle usage.
class SuperCyberSoldierFemale1_AINode_CombatCR : TUtilityBehavior
{
	double MaxDistance; // Actually is squared for performance reasons.
	static SuperCyberSoldierFemale1_AINode_CombatCR Create(double MaxDistance = 1024)
	{
		SuperCyberSoldierFemale1_AINode_CombatCR Result = New('SuperCyberSoldierFemale1_AINode_CombatCR');
		Result.MaxDistance = MaxDistance ** 2;
		return Result;
	}
	override double CalculateUtility(TCustomCyberActor CustomCyberActor)
	{
		TPriorityQueueItem PriorityQueueItem = CustomCyberActor.PriorityQueue.Items[0];
		if (PriorityQueueItem.ExperiencedThreatLevel == ETL_CRITICAL)
			return 0;
		if (PriorityQueueItem.ExperiencedThreatLevel == ETL_DANGER)
			return 0;
		if (CustomCyberActor.Distance2DSquared(CustomCyberActor.Target) <= MaxDistance)
		{
			if ((CustomCyberActor.Target.Health <= 2560) || (CustomCyberActor.Target.Player))
				return 0.5;
			switch (PriorityQueueItem.ThreatLevel)
			{
				case THREAT_UNSTOPPABLE:
					return 0;
					break;
				case THREAT_SUPERDANGER:
					return 0;
					break;
				case THREAT_VERYDANGEROUS:
					return 0;
					break;
				case THREAT_DANGEROUS:
					return 0.5;
					break;
				case THREAT_ABOVENORMAL:
					return 0.5;
					break;
				case THREAT_NORMAL:
					return 0.5;
					break;
				case THREAT_MILD:
					return 0.5;
					break;
				case THREAT_LOW:
					return 0.5;
					break;
				case THREAT_VERYLOW:
					return 0.5;
					break;
				case THREAT_NONE:
					return 0.5;
					break;
				default:
					return 0;
					break;
			}
			return 0;
		}
		return 0;
	}
	override TStatus Update(TCustomCyberActor CustomCyberActor)
	{
		if (CustomCyberActor.WeaponCooldown == 0)
		{
			CustomCyberActor.UpdateStateLabel("Rifle.Fire", true);
			if (CustomCyberActor.CurrentWeapon != 'CyberRifle')
			{
				CustomCyberActor.A_ChangeModel("", 1, "MODELS/SuperCyberSoldierFemale1", "CyberRifle.iqm", 1, "MODELS/CyberRifle", "BaseTexture.png");
				CustomCyberActor.CurrentWeapon = 'CyberRifle';
				CustomCyberActor.WeaponCooldown = 2;
				return BH_RUNNING;
			}
			CustomCyberActor.A_StartSound("weapons/cr", CHAN_WEAPON);
			CustomCyberActor.B_Missile('CyberRifleProjectile', 64);
			CustomCyberActor.WeaponCooldown = 4;
		}
		return BH_SUCCESS;
	}
}

// Node CombatLG: check if fit for PlasmaLaserCannon usage.
class SuperCyberSoldierFemale1_AINode_CombatPLC : TUtilityBehavior
{
	double MaxDistance; // Actually is squared for performance reasons.
	static SuperCyberSoldierFemale1_AINode_CombatPLC Create(double MaxDistance = 1024)
	{
		SuperCyberSoldierFemale1_AINode_CombatPLC Result = New('SuperCyberSoldierFemale1_AINode_CombatPLC');
		Result.MaxDistance = MaxDistance ** 2;
		return Result;
	}
	override double CalculateUtility(TCustomCyberActor CustomCyberActor)
	{
		TPriorityQueueItem PriorityQueueItem = CustomCyberActor.PriorityQueue.Items[0];
		if (PriorityQueueItem.ExperiencedThreatLevel == ETL_CRITICAL)
			return 0.7;
		if (PriorityQueueItem.ExperiencedThreatLevel == ETL_DANGER)
			return 0.7;
		if (CustomCyberActor.Distance2DSquared(CustomCyberActor.Target) <= MaxDistance)
		{
			if (CustomCyberActor.Target.Health > 2560)
				return 0.7;
			switch (PriorityQueueItem.ThreatLevel)
			{
				case THREAT_UNSTOPPABLE:
					return 0.7;
					break;
				case THREAT_SUPERDANGER:
					return 0.7;
					break;
				case THREAT_VERYDANGEROUS:
					return 0.7;
					break;
				case THREAT_DANGEROUS:
					return 0;
					break;
				case THREAT_ABOVENORMAL:
					return 0;
					break;
				case THREAT_NORMAL:
					return 0;
					break;
				case THREAT_MILD:
					return 0;
					break;
				case THREAT_LOW:
					return 0;
					break;
				case THREAT_VERYLOW:
					return 0;
					break;
				case THREAT_NONE:
					return 0;
					break;
				default:
					return 0;
					break;
			}
			return 0;
		}
		return 0;
	}
	override TStatus Update(TCustomCyberActor CustomCyberActor)
	{
		if (CustomCyberActor.WeaponCooldown == 0)
		{
			CustomCyberActor.UpdateStateLabel("Rifle.Fire", true);
			if (CustomCyberActor.CurrentWeapon != 'SuperPlasmaLaserCannon')
			{
				CustomCyberActor.A_ChangeModel("", 1, "MODELS/SuperCyberSoldierFemale1", "PlasmaLaserCannon.iqm", 1, "MODELS/PlasmaLaserCannon", "BaseTexture.png");
				CustomCyberActor.CurrentWeapon = 'SuperPlasmaLaserCannon';
				CustomCyberActor.WeaponCooldown = 2;
				return BH_RUNNING;
			}
			CustomCyberActor.A_StartSound("weapons/splc", CHAN_WEAPON);
			for (int I = 0; I < 24; I++)
				CustomCyberActor.B_Missile('PlasmaLaserCannonProjectile', 64, Random2() * (1.125 / 256));
			CustomCyberActor.WeaponCooldown = 11;
		}
		return BH_SUCCESS;
	}
}

// -----------------------------------------------------------------------------
// SuperCyberSoldierFemale1.
// -----------------------------------------------------------------------------

class SuperCyberSoldierFemale1 : TCyberAdvanced abstract
{
	override void InitBehavior()
	{
		MeleeState = FindState("Melee", true);
		MissileState = FindState("Missile", true);
		AttackStates.Push(MeleeState);
		AttackStates.Push(MissileState);
		AttackStates.Push(FindState("FireBall", true));
		AttackStates.Push(FindState("Hand", true));
		AttackStates.Push(FindState("Hand2", true));
		AttackStates.Push(FindState("KickFront", true));
		AttackStates.Push(FindState("KickLateral", true));
		AttackStates.Push(FindState("Punch", true));
		AttackStates.Push(FindState("QuadPunch", true));
		AttackStates.Push(FindState("Stomp", true));
		AttackStates.Push(FindState("Rifle.Fire", true));
		GestureAnimations.Push(FindState("Dance", true));
		GestureAnimations.Push(FindState("Excited", true));
		GestureAnimations.Push(FindState("Insult", true));
		GestureAnimations.Push(FindState("NoWay", true));
		TBehaviorTreeManager BehaviorTreeManager = TBehaviorTreeManager.GetInstance();
		Behavior = BehaviorTreeManager.GetBehavior(self.GetClass());
		if (!Behavior)
		{
			Behavior = TSequence.Create()
				.AddBehavior(TCyberBasic_AINode_TimeFreeze.Create())
				.AddBehavior(TCyberBasic_AINode_BasicLife.Create())
				.AddBehavior(
					TSelector.Create() // Strategy selector
						.AddBehavior(TCyberBasic_AINode_SearchHelp.Create())
						.AddBehavior(TCyberBasic_AINode_Goal.Create())
						.AddBehavior(TCyberBasic_AINode_Fight.Create())
						.AddBehavior(SuperCyberSoldierFemale1_AINode_Gesture.Create())
						.AddBehavior(SuperCyberSoldierFemale1_AINode_Rest.Create())
				)
				.AddBehavior(
					TSequence.Create()
					.AddBehavior(SuperCyberSoldierFemale1_AINode_ChaseMode.Create(10, 20, 24))
					.AddBehavior(
						TSelector.Create() // Tactical selector
							.AddBehavior(TCyberBasic_AINode_Jump.Create(9))
							.AddBehavior(TCyberBasic_AINode_CoverZone.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_NextMove.Create())
					)
					.AddBehavior(TCyberBasic_AINode_Move.Create())
					.AddBehavior(TCyberBasic_AINode_CloackingDevice.Create())
				)
				.AddBehavior(
					TSequence.Create()
					.AddBehavior(TCyberBasic_AINode_BasicCombat.Create())
					.AddBehavior(
						TUtilitySelector.Create()
							.AddBehavior(SuperCyberSoldierFemale1_AINode_Punch.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_Stomp.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_Fireball.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_Hand.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_Hand2.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_CombatLG.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_CombatCR.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_CombatPLC.Create())
					)
				)
			;
			BehaviorTreeManager.AddBehavior(self.GetClass(), Behavior);
		}
	}
	override void Initialize()
	{
		super.Initialize();
		A_GiveInventory('EnergyShield2', 1);
	}
	virtual void B_IdleNormal()
	{
		CurrentWeapon = null;
		A_ChangeModel("", 1, "MODELS/SuperCyberSoldierFemale1", "NoWeapon.iqm", 1, "", "");
		UpdateStateLabel("Idle");
	}
	virtual void B_MissileFireballCharge()
	{
		if ( Target )
			A_SpawnItemEx("KiBallCharging", +24, -24, 72);
	}
	virtual void B_MissileFireball()
	{
		if (Target)
		{
			B_Missile("LargeKiBall", 64);
		}
	}
	virtual void B_MissileHand()
	{
		if ( Target )
		{
			B_Missile("CyberBallLarge", 64);
		}
	}
	virtual void B_MissileHand2()
	{
		if ( Target )
		{
			B_Missile("SmallKiBall", 40);
		}
	}
	virtual void B_Kick()
	{
		if ( Target )
			A_CustomMeleeAttack(Random(1, 8) * 64, "SuperSoldierFemale/Melee");
	}
	virtual void B_Punch()
	{
		if ( Target )
			A_CustomMeleeAttack(Random(1, 8) * 16, "SuperSoldierFemale/Melee");
	}
	virtual void B_Stomp()
	{
		if ((Target) && (Target.Height <= 60) && (Target.default.Health) <= 256 && (Target.default.Health > 0))
		{
			if ( Distance2D(Target) <= Radius + Target.Radius + 40 )
			{
				Target.GiveInventory('StompToken', 1);
				Target.DamageMobj(self, self, Target.Health, "Normal", DMG_FORCED);
			}
		}
	}
	virtual void B_Pain()
	{
		A_Pain();
		A_GiveInventory('ShieldBoost', 1);
	}
	default
	{
		CameraHeight 80;
		DropItem "CyberBattery", 192;
		DropItem "CyberBatteryPack", 32;
		DropItem "LightningGun", 64;
		DropItem "CyberRifle", 64;
		DropItem "SuperPlasmaLaserCannon", 64;
		DropItem "HyperCyberArmor", 11;
		Health 625;
		Height 80;
		Mass 10000;
		MeleeRange 80;
		PainChance 80;
		Radius 28;
		Speed 12;
		Tag "Super Cyber Soldier Female LVL 1";
		KAI_Actor.AggressionMultiplier 0.5;
		KAI_Actor.ThreatLevel THREAT_SUPERDANGER;
		KAI_Actor.ThreatLevelThreshold THREAT_SUPERDANGER;
		ActiveSound "SuperSoldierFemale/Active";
		DeathSound "SuperSoldierFemale/Death";
		PainSound "SuperSoldierFemale/Pain";
		SeeSound "SuperSoldierFemale/Sight";
	}
	states
	{
		Spawn:
			TNT1 A 0;
			goto Think;
		See:
			---- A 0;
			goto Think;
		Melee:
			---- A 0;
			goto Think;
		Missile:
			---- A 0;
			goto Think;
		Pain:
			---- A 0 B_Pain();
			goto Think;
		Death:
			NDE0 A 1 B_Die();
			NDE0 BCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NDE1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NDE2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NDE3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NDE4 AB 1;
			NDE4 C -1;
			stop;
		Raise:
			NRA0 A 1 Initialize();
			NRA0 BCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NRA1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NRA2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NRA3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NRA4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NRA5 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NRA6 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NRA7 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NRA8 ABCDEFGHIJKLMNOPQRSTU 1;
			goto Think;
		Think:
			---- A 0 B_Think();
			loop; // Never, it is safe!
		Dance:
			NDA0 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NDA1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NDA2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NDA3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NDA4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NDA5 ABCDEFGHIJKLMNOPQR 1;
			goto Think;
		Excited:
			NEX0 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NEX1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NEX2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NEX3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NEX4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NEX5 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NEX6 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NEX7 ABCDEFGHIJKLMNOP 1;
			goto Think;
		FireBall:
			NFB0 A 1 B_MissileFireballCharge();
			NFB0 BCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_FaceTarget();
			NFB1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_FaceTarget();
			NFB2 ABCDEFGHIJKL 1 A_FaceTarget();
			NFB2 M 1 B_MissileFireball();
			NFB2 NOPQRSTUVWXYZ 1 A_FaceTarget();
			NFB3 ABCDEFGHIJKLMNOPQRSTUVWX 1 A_FaceTarget();
			goto Think;
		Hand:
			NHR0 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_FaceTarget();
			NHR1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_FaceTarget();
			NHR2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_FaceTarget();
			NHR3 A 1 B_MissileHand();
			NHR3 BCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_FaceTarget();
			NHR4 ABCDEFGHIJKLMNOPQRS 1 A_FaceTarget();
			goto Think;
		Hand2:
			NH20 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_FaceTarget();
			NH21 ABCDEFGHIJ 1 A_FaceTarget();
			NH21 KLMNOPQRSTUVWXYZ 1 B_MissileHand2();
			NH22 ABCDEFGH 1 B_MissileHand2();
			NH22 IJKLMNOPQRSTUVWXYZ 1 A_FaceTarget();
			NH23 ABCD 1 A_FaceTarget();
			goto Think;
		Heal:
			NHE0 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NHE1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NHE2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NHE3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NHE4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NHE5 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NHE6 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NHE7 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NHE8 ABCDEFGHIJKLMNOPQ 1;
			goto Think;
		Idle:
			NID0 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NID1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NID2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NID3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NID4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NID5 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NID6 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NID7 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NID8 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NID9 ABCDEFGHIJKLMNOPQ 1;
			#### # 0 PlayActiveSound();
			goto Think;
		Insult:
			NIN0 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NIN1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NIN2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NIN3 ABC 1;
			goto Think;
		Jump:
			NJU0 A 1 B_JumpBegin();
			NJU0 BCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NJU1 A 1 B_JumpEnd();
			goto Think;
		KickFront:
			NKF0 ABCDEFGHIJKLMNOPQRSTU 1 B_MoveForward();
			NKF0 V 1 B_Kick();
			NKF0 WXYZ 1 B_MoveBackward();
			NKF1 ABCDEFGHIJKLMNOPQRSTUVW 1 B_MoveBackward();
			goto Think;
		KickLateral:
			NKL0 ABCDEFGHIJKLMNOPQRSTU 1 B_MoveForward();
			NKL0 V 1 B_Kick();
			NKL0 WXYZ 1 B_MoveBackward();
			NKL1 ABCDEFGHIJKLMNOPQRSTUVWXY 1 B_MoveBackward();
			goto Think;
		Noway:
			NNO0 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NNO1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NNO2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NNO3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NNO4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NNO5 ABCDEFGHIJKLMNOPQRSTU 1;
			goto Think;
		Punch:
			NPU0 ABCDEFGHIJ 1 B_MoveForward();
			NPU0 K 1 B_Punch();
			NPU0 LMNOPQRSTUVWXYZ 1 B_MoveBackward();
			NPU1 A 1 B_MoveBackward();
			goto Think;
		QuadPunch:
			NQP0 ABCDEFGHIJKLMNOP 1 B_MoveForward();
			NQP0 Q 1 B_Punch();
			NQP0 RSTU 1 B_MoveForward();
			NQP0 V 1 B_Punch();
			NQP0 WXYZ 1 B_MoveForward();
			NQP1 ABCD 1 B_MoveForward();
			NQP1 E 1 B_Punch();
			NQP1 FGHI 1 B_MoveForward();
			NQP1 J 1 B_Punch();
			NQP1 KLMNOPQRSTUVWXYZ 1 B_MoveBackward();
			NQP2 ABCDEFGHIJKLMN 1 B_MoveBackward();
			goto Think;
		Run:
			NRU0 ABCDEFGHIJKLMNOPQRSTUV 1;
			goto Think;
		RunFast:
			NRF0 ABCDEFGHIJKLMNOPQRS 1;
			goto Think;
		Stomp:
			NST0 ABCDEFGHIJKLMN 1 B_MoveForward();
			NST0 O 1 A_ChangeVelocity(0, 0, 12, CVF_RELATIVE);
			NST0 PQRSTUVWXYZ 1 B_MoveForward();
			NST1 A 1 B_Stomp();
			NST1 BCDEFGHIJKLMNOPQRSTUVWXY 1;
			goto Think;
		Swim:
			NSW0 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NSW1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NSW2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NSW3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NSW4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NSW5 ABCDEFG 1;
			goto Think;
		Walk:
			NWA0 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NWA1 ABCDEFGHIJ 1;
			goto Think;
		Water:
			NWT0 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NWT1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NWT2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			NWT3 ABCDEFGHIJKLM 1;
			goto Think;
		Rifle.Idle:
			WID0 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			WID1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			WID2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			WID3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			WID4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			WID5 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			WID6 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			WID7 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			WID8 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
			WID9 ABCDEFGHIJKLMNOPQRSTUVWX 1;
			#### # 0 B_IdleNormal();
			goto Think;
		Rifle.Run:
			WRU0 ABCDEFGHIJKLMNOPQR 1;
			goto Think;
		Rifle.Fire:
			WFI0 A 1 A_Jump(16, "Think");
			WFI0 BCD 1 A_FaceTarget();
			WFI0 E 1 A_MonsterRefire(0, "Think");
			goto Rifle.Fire;
	}
}
class SuperCyberSoldierFemale1BlueMetallic   : SuperCyberSoldierFemale1 {}
class SuperCyberSoldierFemale1PinkMetallic   : SuperCyberSoldierFemale1 {}
class SuperCyberSoldierFemale1RedMetallic    : SuperCyberSoldierFemale1 {}
class SuperCyberSoldierFemale1RedMilk        : SuperCyberSoldierFemale1 {}
class SuperCyberSoldierFemale1SandPaint      : SuperCyberSoldierFemale1 {}
class SuperCyberSoldierFemale1WhiteMetallic  : SuperCyberSoldierFemale1
{
	override void InitBehavior()
	{
		MeleeState = FindState("Melee", true);
		MissileState = FindState("Missile", true);
		AttackStates.Push(MeleeState);
		AttackStates.Push(MissileState);
		AttackStates.Push(FindState("FireBall", true));
		AttackStates.Push(FindState("Hand", true));
		AttackStates.Push(FindState("Hand2", true));
		AttackStates.Push(FindState("KickFront", true));
		AttackStates.Push(FindState("KickLateral", true));
		AttackStates.Push(FindState("Punch", true));
		AttackStates.Push(FindState("QuadPunch", true));
		AttackStates.Push(FindState("Stomp", true));
		AttackStates.Push(FindState("Rifle.Fire", true));
		GestureAnimations.Push(FindState("Dance", true));
		GestureAnimations.Push(FindState("Excited", true));
		GestureAnimations.Push(FindState("Insult", true));
		GestureAnimations.Push(FindState("NoWay", true));
		TBehaviorTreeManager BehaviorTreeManager = TBehaviorTreeManager.GetInstance();
		Behavior = BehaviorTreeManager.GetBehavior(self.GetClass());
		if (!Behavior)
		{
			Behavior = TSequence.Create()
				.AddBehavior(TCyberBasic_AINode_TimeFreeze.Create())
				.AddBehavior(TCyberBasic_AINode_BasicLife.Create())
				.AddBehavior(
					TSelector.Create() // Strategy selector
						.AddBehavior(TCyberBasic_AINode_SearchHelp.Create())
						.AddBehavior(TCyberBasic_AINode_GiveAid.Create())
						.AddBehavior(TCyberBasic_AINode_Goal.Create())
						.AddBehavior(TCyberBasic_AINode_Fight.Create())
						.AddBehavior(SuperCyberSoldierFemale1_AINode_Gesture.Create())
						.AddBehavior(SuperCyberSoldierFemale1_AINode_Rest.Create())
				)
				.AddBehavior(
					TSequence.Create()
					.AddBehavior(SuperCyberSoldierFemale1_AINode_ChaseMode.Create(10, 20, 24))
					.AddBehavior(
						TSelector.Create() // Tactical selector
							.AddBehavior(TCyberBasic_AINode_Jump.Create(9))
							.AddBehavior(TCyberBasic_AINode_CoverZone.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_NextMove.Create())
					)
					.AddBehavior(TCyberBasic_AINode_Move.Create())
					.AddBehavior(TCyberBasic_AINode_CloackingDevice.Create())
				)
				.AddBehavior(
					TSequence.Create()
					.AddBehavior(TCyberBasic_AINode_BasicCombat.Create())
					.AddBehavior(
						TUtilitySelector.Create()
							.AddBehavior(SuperCyberSoldierFemale1_AINode_Punch.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_Stomp.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_Fireball.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_Hand.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_Hand2.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_CombatLG.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_CombatCR.Create())
							.AddBehavior(SuperCyberSoldierFemale1_AINode_CombatPLC.Create())
					)
				)
			;
			BehaviorTreeManager.AddBehavior(self.GetClass(), Behavior);
		}
	}
	default
	{
		Tag "Super Cyber Soldier Female LVL 1 (Nurse)";
		+TCustomNavMeshActor.NavMeshNurse;
	}
}
class SuperCyberSoldierFemale1YellowPaint    : SuperCyberSoldierFemale1 {}
